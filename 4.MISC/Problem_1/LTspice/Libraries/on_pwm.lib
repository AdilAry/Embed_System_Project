* Library of ON Semiconductor PWM Models

* $Revision:   1.0  $
* $Author:   HIRASUNA  $
* $Date:   20 Jul 2001 18:37:58  $
* ------------------------------------------------------------ *
*
* ON Semiconductor
* MC33363 model developed by Christophe BASSO, Toulouse (FRANCE)
* email: christophe.basso@onsemi.com
* OrCAD's PSpice compatible
*
* Last modified: 5/13/01
* UVLO section has been re-written - No hysteresis on S switch 
* If you experience convergence problems, increase ITL4 to 20
*
* ------------------------------------------------------------ *
***********
.SUBCKT MC33363/ON 1   3   4   6  7   8   9   10  11   16 
+ PARAMS: RDSON=14
*                 STR  VCC GND RT CT  REG COMP VF OVL  DRAIN
**** ISUPPLY ****
GSUPP 3 4 VALUE = { IF( V(33)<3.5, 250U, 3.5M ) }
**** REFERENCE VOLTAGE ****
EREF 80 4 VALUE = { IF( V(3)<7.7, V(3)-1.2, 6.5 ) }
ROUT 80 8 10
**** STARTUP AND UVLO ****
XUVLO 3 81 UVLO_33363
XOVL 60 11 82 COMP2_33363
GSTRT 1 3 VALUE = { IF( V(81)<3.5, (14M*((14-V(3))/14)+6M), 40U ) }
XAND 81 82 33 AND2_33363
**** OSCILLATOR SECTION ****
GCHG 4 7 VALUE = { IF( ((V(INV1)>500M) & (V(33)>3.5)), I(VIRT)*4, 0 ) }
GDIS 7 4 VALUE = { IF( V(INV1)<500M, I(VIRT)*4, 0 ) }
DUV 7 33 DUV
VSET 21 4 1.25V
VIRT 21 6
VREF1 22 4 2.5V
RI 22 23 40K
RF 23 INV1 10K
ECMP CLK 4 VALUE = { IF( (V(7)-V(23)>0), 5, 0 ) }
EINV 25 4 VALUE = { IF( V(CLK)<500M, 2.5, 0 ) }
RDEL1 25 INV1 100
CEL2 INV1 4 100P
**** PWM AND LATCH ****
XPWM 7 9 31 COMP2_33363
XOR1 33 31 32 34 OR3_IN_INV_33363
XNAND1 34 CLK 35 AND_IN_INV_33363
XFFLOP 35 34 36 37 FFLOP_33363
XNAND2 CLK 36 33 450 3 AND3_IN_INV_33363
RDRV 450 DRV 100
**** ERROR AMPLIFIER ****
VREF2 60 4 2.6V
XERR 60 10 9 4 ERRAMP_33363 ; +  - OUT GND
**** CURRENT LIMIT COMPARATOR ****
RLIMIT 40 4 450
G225 4 40 VALUE = { I(VIRT)*2.25 }
XCOMP 50 40 32 COMPDEL_33363
**** MOSFET MODEL ****
CIN DRV 4 200pF
SMOS 16 71 DRV 4 MOS
VISRC 71 4
**** KELVIN AND L.E.B. ****
GIKEL 4 50 VALUE = { IF( V(DRV)>V(3)-0.7, I(VISRC)/51.2, 0 ) }
RKEL 50 4 9
.MODEL MOS VSWITCH (RON={RDSON} ROFF=1MEG VON=3 VOFF=1)
.MODEL DUV D RS=100M TT=50N CJO=10P N=0.1
.ENDS
**** DELAYED COMPARATOR ****
.SUBCKT COMPDEL_33363 1 2 3
*                          + - Out
E1 4 0 VALUE = { IF( V(1) > V(2), 1V, 0 ) }
RT 4 5 100
CT 5 0 3.36NF
E2 10 0 VALUE = { IF( V(5) > 500M, 5V, 0 ) }
RD 10 3 100
CD 3 0 100P
.ENDS 
**** UVLO CIRCUIT ****
.SUBCKT UVLO_33363  1   3  
*                   VIN OUT
Vref 10 0 5V
X1 10 3 1 0 SWhyste_33363 params: RON=1 ROFF=1MEG VT=12 VH=2.5
RUV 3 0 1k
.ENDS 
**** T FFLOP Circuit ****
.SUBCKT FFLOP_33363 6 8 2 1
*                   S R Q Q\
EQB 10 0 VALUE = { IF ( (V(8)<800M) & (V(2)>800M), 0, 5V ) }
EQ  20 0 VALUE = { IF ( (V(6)<800M) & (V(1)>800M), 0, 5V ) }
RD1   10 1 100
CD1   1 0 10P IC=5
RD2   20 2 100
CD2   2 0 10P IC=0
.ENDS 
**** 2 INPUT, ONE INPUT IS INV, AND CIRCUIT ****
.SUBCKT AND_IN_INV_33363 1   2    3
*                        INV NINV OUT
E1 4 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 3 INPUT, ONE INPUT IS INV. AND CIRCUIT ****
.SUBCKT AND3_IN_INV_33363  1   2    3    4  6
*                         INV NINV NINV OUT VCC 
E1 5 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M) & (V(3)>800M)), V(6), 0 ) }
RD 5 4 10
CD 4 0 100P
.ENDS 
**** 3 INPUT, ONE INPUT IS INV. OR CIRCUIT ****
.SUBCKT OR3_IN_INV_33363 1   2    3    4
*                       INV NINV NINV OUT
E1 5 0 VALUE = { IF( ((V(1)<3.5V) | (V(2)>800M) | (V(3)>800M)), 5V, 0 ) }
RD 5 4 100
CD 4 0 10P
.ENDS 
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2_33363 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 2 INPUT NOR CIRCUIT ****
.SUBCKT NOR2_33363 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) | (V(2)>800M)), 0V, 5 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2_33363 1 2 3
*                   + - S
E1 4 0 VALUE = { IF( (V(1) > V(2)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** ERROR AMPLIFIER MODEL ****
.SUBCKT ERRAMP_33363 20 8  3  21
*   	               +  - OUT GND
RIN 20 8 8MEG
CP1 11 21 11.5P	; {1/(6.28*(GAIN/100U)*POLE)}
E1 5 21 11 21 1
R9 5 2 5
D14 2 13 DMOD
ISINK 13 21 2.7U	; {ISINK/100} in mA
Q1 21 13 16 QPMOD
ISOURCE 7 3 100u ; {ISOURCE} in uA
D12 3 7 DMOD
D15 21 11 DCLAMP
G1 21 11 20 8 100U
V1 7 21 5.8			; Vhigh - 0.6V
V4 3 16 250M		; Vlow - 38mV
RP1 11 21 125.9MEG	; {GAIN/100U}
.MODEL QPMOD PNP
.MODEL DCLAMP D (RS=10 BV=5 IBV=0.01)
.MODEL DMOD D TT=1N CJO=10P
.ENDS 
****
.subckt SWhyste_33363 NodeMinus NodePlus Plus Minus PARAMS: RON=1 ROFF=1MEG
+ VT=5 VH=2
S5 NodePlus NodeMinus 8 0 smoothSW
EBcrtl 8 0 Value = { IF ( V(plus)-V(minus) > V(ref), 1, 0 ) }
EBref ref1 0 Value = { IF ( V(8) > 0.5, {VT-VH}, {VT+VH} ) }
Rdel1 ref1 ref 100
Cdel1 ref 0 100p  IC={VT+VH}
Rconv1 8 0 10Meg
Rconv2 plus 0 10Meg
Rconv3 minus 0 10Meg
.model smoothSW VSWITCH (RON={RON} ROFF={ROFF} VON=1 VOFF=0)
.ends 
***** END OF MC33363 INTERNAL MODELS *****
*****
* ------------------------------------------------------------ *
*
* ON Semiconductor
* MC33364 model developed by Christophe BASSO, Toulouse (FRANCE)
* e-mail: christophe.basso@onsemi.com
* OrCAD's PSpice compatible
*
* These models account for the various propagation delays and
* timers, except the internal 100ms re-start delay which has
* NOT purposely been included.
*
* Last modified: 5/14/01
*
* MC33364D1: fixed internal frequency clamp
* MC33364D2: no internal frequency clamp
* MC33364D: adjustable frequency clamp (Not available now)
*
* UVLO section has been re-written - No hysteresis on S switch 
* If you experience convergence problems, increase ITL4 to 20
*
* ------------------------------------------------------------ *
**********
.SUBCKT MC33364D1/ON 1  2  3  4   5   6   7   8
*                    ZC CS FB Ref Gnd Drv Vcc Line
**** ISUPPLY ****
GSUPP 7 5 VALUE = { IF ( V(81)<3.5, 800U, 4.5M ) }
**** REFERENCE VOLTAGE ****
EREF 80 5 VALUE = { IF (V(81)<3.5, 0, 5V ) }
ROUT 80 4 10
RBIAS 4 3 5k
**** STARTUP AND UVLO ****
XUVLO 7 81 UVLO_33364
GSTRT 8 7 VALUE = { IF ( V(81)<3.5, (8.5M*((14-V(7))/14)+6M), 70U ) }
CSTRT 81 600 2nF
RSTRT 5 600 1k
XOR2 600 213 610 OR2_33364
**** ZERO DETECTION ****
DCLAMP 5 1 DZEN1
XCOMP1 50 1 51 COMP2_33364
XDEL1 51 52 UTD_33364 PARAMS: TD={300ns}
EREF1 50 5 VALUE = { IF ( V(51) > 1V, 1.2V, 1V ) }
**** CURRENT SENSE ****
RCS1 3 30 45k
RCS2 30 5 15k
DCS 5 30 DZEN2
XCOMP2 32 30 31 COMP2_33364 ; + - S
XDEL2 31 33 UTD_33364 PARAMS: TD={230ns}
VOFF1 32 LEBO 100mV
XLATCH 33 302 620 661 TFFLOP_33364 ; S R Q Qb
RDUM1 661 5 100k
**** 250ns LEB ****
XDEL3 121 62 UTD_33364 PARAMS: TD={250ns}
ELEB LEBO 5 VALUE = { IF (V(62) > 3V, V(2), 0 ) }
**** LOGIC CIRCUITRY ****
XFFLOP 52 200 620 610 120 121 F_FLOP_33364
RDUM2 120 5 100k
VDAT 200 5 5
**** 400us TIMER ****
GCHG 5 211 VALUE = { IF (V(121) < 3V, 10uA, 0 ) }
CTIM 211 5 410pF
VTIM 212 5 10V
XCOMP3 211 212 213 COMP2_33364
DRST 211 120 DSTD
**** FIXED 6.7us DEADTIME ****
ICHG 5 300 3uA
CDT 300 5 10pF
XCOMP4 300 301 302 COMP2_33364
VDT 301 5 2V
S2 300 5 121 5 SDT_33364
**** DRIVER and UVLO ****
EDRVU 90 5 VALUE = { IF ( (V(121)>3V) & (V(81)>3V), V(7), 100mV ) }
RON 90 6 36
ROFF 6 250 18
DOFF 6 250 DSTD
.MODEL SDT_33364 VSWITCH (RON=1 ROFF=10MEG VON=3 VOFF=1)
.MODEL DSTD D RS=100M CJO=15pF TT=10N IBV=100u N=0.01
.MODEL DZEN2 D BV=1.2 RS=100M CJO=15pF TT=100N IBV=100u
.MODEL DZEN1 D BV=10 RS=100M CJO=15pF TT=100N IBV=1m
.ENDS 
***************
.SUBCKT MC33364D2/ON 1  2  3  4   5   6   7   8
*                    ZC CS FB Ref Gnd Drv Vcc Line
**** ISUPPLY ****
GSUPP 7 5 VALUE = { IF ( V(81)<3.5, 800U, 4.5M ) }
**** REFERENCE VOLTAGE ****
EREF 80 5 VALUE = { IF (V(81)<3.5, 0, 5V ) }
ROUT 80 4 10
RBIAS 4 3 5k
**** STARTUP AND UVLO ****
XUVLO 7 81 UVLO_33364
GSTRT 8 7 VALUE = { IF ( V(81)<3.5, (8.5M*((14-V(7))/14)+6M), 70U ) }
CSTRT 81 600 2nF
RSTRT 5 600 1k
XOR2 600 213 610 OR2_33364
**** ZERO DETECTION ****
DCLAMP 5 1 DZEN1
XCOMP1 50 1 51 COMP2_33364
XDEL1 51 52 UTD_33364 PARAMS: TD={300ns}
EREF1 50 5 VALUE = { IF ( V(51) > 1V, 1.2V, 1V ) }
**** CURRENT SENSE ****
RCS1 3 30 45k
RCS2 30 5 15k
DCS 5 30 DZEN2
XCOMP2 32 30 31 COMP2_33364 ; + - S
XDEL2 31 33 UTD_33364 PARAMS: TD={230ns}
VOFF1 32 LEBO 100mV
**** 250ns LEB ****
XDEL3 121 62 UTD_33364 PARAMS: TD={250ns}
ELEB LEBO 5 VALUE = { IF (V(62) > 3V, V(2), 0 ) }
**** LOGIC CIRCUITRY ****
XFFLOP 52 200 33 610 120 121 F_FLOP_33364
RDUM2 120 5 100k
VDAT 200 5 5
**** 400us TIMER ****
GCHG 5 211 VALUE = { IF (V(121) < 3V, 10uA, 0 ) }
CTIM 211 5 410pF
VTIM 212 5 10V
XCOMP3 211 212 213 COMP2_33364
DRST 211 120 DSTD
**** DRIVER and UVLO ****
EDRVU 90 5 VALUE = { IF ( (V(121)>3V) & (V(81)>3V), V(7), 100mV ) }
RON 90 6 36
ROFF 6 250 18
DOFF 6 250 DSTD
.MODEL SDT_33364 VSWITCH (RON=1 ROFF=10MEG VON=3 VOFF=1)
.MODEL DSTD D RS=100M CJO=15pF TT=10N IBV=100u N=0.01
.MODEL DZEN2 D BV=1.2 RS=100M CJO=15pF TT=100N IBV=100u
.MODEL DZEN1 D BV=10 RS=100M CJO=15pF TT=100N IBV=1m
.ENDS 
********************
.SUBCKT UTD_33364 1 2 PARAMS: TD=100N
RIN 1 0 1E15
E1 3 0 1 0 1
T1 3 0 2 0 Z0=1 TD={TD}
R1 2 0 1
.ENDS 
**** UVLO CIRCUIT ****
.SUBCKT UVLO_33364  1   3  
*                  VIN OUT
Vref 10 0 5V
X1 10 3 1 0 SWhyste_33364 params: RON=1 ROFF=1MEG VT=11.3 VH=3.7
RUV 3 0 1k
.ENDS 
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2_33364 1 2 3
*                   + - S
E1 4 0 VALUE = { IF ( V(1) > V(2), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 2 INPUT OR GATE ****
.SUBCKT OR2_33364 1 2 3
E1 4 0 VALUE = { IF ( (V(1)>800M) | (V(2)>800M), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*****
.SUBCKT F_FLOP_33364 1  2 11 12 5  6
*                   CLK D R  S  QB Q 
X1 7 4 2 8 NAND30_33364
X2 8 3 10 9 NAND30_33364
X3 1 8 10 7 NAND31_33364
X4 4 9 1 10 NAND30_33364
X5 4 7 6 5 NAND31_33364
X6 5 10 3 6 NAND30_33364
X7 11 4 INV_33364
X8 12 3 INV_33364
.ENDS 
***** INTERNAL F_FLOP DEFINITION ****
.SUBCKT NAND30_33364 1 2 3 4
E1 5 0 VALUE = { IF ( (V(1)>800mV) & (V(2)>800mV) & (V(3)>800mV), 100m, 10 ) }
R1 5 4 10
C1 4 0 10P IC=100m
.ENDS 
.SUBCKT NAND31_33364 1 2 3 4
E1 5 0 VALUE = { IF ( (V(1)>800mV) & (V(2)>800mV) & (V(3)>800mV), 100m, 10 ) }
R1 5 4 10
C1 4 0 10P IC=10
.ENDS 
.SUBCKT INV_33364 1 2
E1 3 0 VALUE = { IF ( V(1)>800mV, 100m, 10 ) }
R1 3 2 10
C1 2 0 10P IC=10
.ENDS 
**** T FFLOP Circuit ****
.SUBCKT TFFLOP_33364 6 8 2 1
*                    S R Q Q\
EQB 10 0 VALUE = { IF ( (V(8)<800M) & (V(2)>800M), 0, 5V ) }
EQ  20 0 VALUE = { IF ( (V(6)<800M) & (V(1)>800M), 0, 5V ) }
RD1   10 1 100
CD1   1 0 10P IC=5
RD2   20 2 100
CD2   2 0 10P IC=0
.ENDS 
*******
.subckt SWhyste_33364 NodeMinus NodePlus Plus Minus PARAMS: RON=1 ROFF=1MEG 
+VT=5 VH=2
S5 NodePlus NodeMinus 8 0 smoothSW
EBcrtl 8 0 Value = { IF ( V(plus)-V(minus) > V(ref), 1, 0 ) }
EBref ref1 0 Value = { IF ( V(8) > 0.5, {VT-VH}, {VT+VH} ) }
Rdel1 ref1 ref 100
Cdel1 ref 0 100p  IC={VT+VH}
Rconv1 8 0 10Meg
Rconv2 plus 0 10Meg
Rconv3 minus 0 10Meg
.model smoothSW VSWITCH (RON={RON} ROFF={ROFF} VON=1 VOFF=0)
.ends 
***** END OF MC33364 INTERNAL MODELS *****
* ------------------------------------------------------------ *
*
* ON Semiconductor
* MC33365 model developed by Christophe BASSO, Toulouse (FRANCE)
* email: christophe.basso@onsemi.com
* OrCAD's PSpice compatible
*
* Last modified: 15/5/01
* UVLO section has been re-written - No hysteresis on S switch
* If you experience convergence problem, increase ITL4 to 20
*
* ------------------------------------------------------------ *
***********
.SUBCKT MC33365/ON 1   3   4   5  6  7   8    9  10  11  16
+ PARAMS: RDSON=15
*                 STR VCC GND RT CT REG COMP VF BOK GND  DRAIN
**** ISUPPLY ****
GSUPP 3 4 VALUE = { IF( V(33)<3.5, 250U, 3.5M ) }
**** REFERENCE VOLTAGE ****
EREF 80 4 VALUE = { IF( V(3)<7.7, V(3)-1.2, 6.5 ) }
ROUT 80 8 10
**** STARTUP AND UVLO ****
XUVLO 3 81 UVLO_33365
GBOK 3 11 VALUE = { IF( V(82)>3.5, 50U, 100n ) }
VBOK 120 4 1.25
XOVL 11 120 82 COMP2_33365
GSTRT 1 3 VALUE = { IF( V(81)<3.5, (0.2M*((14-V(3))/14)+1.6M), 40U ) }
XAND 81 82 33 AND2_33365
**** OSCILLATOR SECTION ****
GCHG 4 7 VALUE = { IF( ((V(INV1)>500M) & (V(33)>3.5)), I(VIRT)*4, 0 ) }
GDIS 7 4 VALUE = { IF( V(INV1)<500M, I(VIRT)*4, 0 ) }
DUV 7 33 DUV
VSET 21 4 1.25V
VIRT 21 6
VREF1 22 4 2.5V
RI 22 23 40K
RF 23 INV1 10K
ECMP CLK 4 VALUE = { IF( (V(7)-V(23)>0), 5, 0 ) }
EINV 25 4 VALUE = { IF( V(CLK)<500M, 2.5, 0 ) }
RDEL1 25 INV1 100
CEL2 INV1 4 100P
**** PWM AND LATCH ****
XPWM 7 9 31 COMP2_33365
XOR1 33 31 32 34 OR3_IN_INV_33365
XNAND1 34 CLK 35 AND_IN_INV_33365
XFFLOP 35 34 36 37 FFLOP_33365
XNAND2 CLK 36 33 450 3 AND3_IN_INV_33365
RDRV 450 DRV 100
**** ERROR AMPLIFIER ****
VREF2 60 4 2.6V
XERR 60 10 9 4 ERRAMP_33365 ; +  - OUT GND
**** CURRENT LIMIT COMPARATOR ****
RLIMIT 40 4 450
G225 4 40 VALUE = { I(VIRT)*2.25 }
XCOMP 50 40 32 COMPDEL_33365
**** MOSFET MODEL ****
CIN DRV 4 200pF
SMOS 16 71 DRV 4 MOS
.MODEL MOS VSWITCH (RON={RDSON} ROFF=1MEG VON=3 VOFF=1)
VISRC 71 4
**** KELVIN AND L.E.B. ****
GIKEL 4 50 VALUE = { IF( V(DRV)>V(3)-0.7, I(VISRC)/51.2, 0 ) }
RKEL 50 4 9
.MODEL DUV D RS=100M TT=50N CJO=10P N=0.1
.ENDS 
**** DELAYED COMPARATOR ****
.SUBCKT COMPDEL_33365 1 2 3
*               + - Out
E1 4 0 VALUE = { IF( V(1) > V(2), 1V, 0 ) }
RT 4 5 100
CT 5 0 3.36NF
E2 10 0 VALUE = { IF( V(5) > 500M, 5V, 0 ) }
RD 10 3 100
CD 3 0 100P
.ENDS 
**** UVLO CIRCUIT ****
.SUBCKT UVLO_33365  1   3  
*            VIN OUT
Vref 10 0 5V
X1 10 3 1 0 SWhyste_33365 params: RON=1 ROFF=1MEG VT=12 VH=2.5
RUV 3 0 1k
.ENDS 
**** T FFLOP Circuit ****
.SUBCKT FFLOP_33365 6 8 2 1
*             S R Q Q\
EQB 10 0 VALUE = { IF ( (V(8)<800M) & (V(2)>800M), 0, 5V ) }
EQ  20 0 VALUE = { IF ( (V(6)<800M) & (V(1)>800M), 0, 5V ) }
RD1   10 1 100
CD1   1 0 10P IC=5
RD2   20 2 100
CD2   2 0 10P IC=0
.ENDS 
**** 2 INPUT, ONE INPUT IS INV, AND CIRCUIT ****
.SUBCKT AND_IN_INV_33365 1   2    3
*                       INV NINV OUT
E1 4 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 3 INPUT, ONE INPUT IS INV. AND CIRCUIT ****
.SUBCKT AND3_IN_INV_33365  1   2    3    4  6
*                         INV NINV NINV OUT VCC 
E1 5 0 VALUE = { IF( ((V(1)<3.5V) & (V(2)>800M) & (V(3)>800M)), V(6), 0 ) }
RD 5 4 10
CD 4 0 100P
.ENDS 
**** 3 INPUT, ONE INPUT IS INV. OR CIRCUIT ****
.SUBCKT OR3_IN_INV_33365 1   2    3    4
*                       INV NINV NINV OUT
E1 5 0 VALUE = { IF( ((V(1)<3.5V) | (V(2)>800M) | (V(3)>800M)), 5V, 0 ) }
RD 5 4 100
CD 4 0 10P
.ENDS 
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2_33365 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) & (V(2)>800M)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 2 INPUT NOR CIRCUIT ****
.SUBCKT NOR2_33365 1 2 3
E1 4 0 VALUE = { IF( ((V(1)>800M) | (V(2)>800M)), 0V, 5 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2_33365 1 2 3
*                   + - S
E1 4 0 VALUE = { IF( (V(1) > V(2)), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
**** ERROR AMPLIFIER MODEL ****
.SUBCKT ERRAMP_33365 20 8  3  21
*   	               +  - OUT GND
RIN 20 8 8MEG
CP1 11 21 11.5P	; {1/(6.28*(GAIN/100U)*POLE)}
E1 5 21 11 21 1
R9 5 2 5
D14 2 13 DMOD
ISINK 13 21 2.7U	; {ISINK/100} in mA
Q1 21 13 16 QPMOD
ISOURCE 7 3 100u ; {ISOURCE} in uA
D12 3 7 DMOD
D15 21 11 DCLAMP
G1 21 11 20 8 100U
V1 7 21 5.8			; Vhigh - 0.6V
V4 3 16 250M		; Vlow - 38mV
RP1 11 21 125.9MEG	; {GAIN/100U}
.MODEL QPMOD PNP
.MODEL DCLAMP D (RS=10 BV=5 IBV=0.01)
.MODEL DMOD D TT=1N CJO=10P
.ENDS 
*******
.subckt SWhyste_33365 NodeMinus NodePlus Plus Minus PARAMS: RON=1 ROFF=1MEG 
+VT=5 VH=2
S5 NodePlus NodeMinus 8 0 smoothSW
EBcrtl 8 0 Value = { IF ( V(plus)-V(minus) > V(ref), 1, 0 ) }
EBref ref1 0 Value = { IF ( V(8) > 0.5, {VT-VH}, {VT+VH} ) }
Rdel1 ref1 ref 100
Cdel1 ref 0 100p IC={VT+VH}
Rconv1 8 0 10Meg
Rconv2 plus 0 10Meg
Rconv3 minus 0 10Meg
.model smoothSW VSWITCH (RON={RON} ROFF={ROFF} VON=1 VOFF=0)
.ends 
***** END OF MC33365 INTERNAL MODELS *****
* ------------------------------------------------------------ *
*
* ON Semiconductor
* NCP1200 model developed by Christophe BASSO, Toulouse (FRANCE)
* e-mail: christophe.basso@onsemi.com
* OrCAD PSpice compatible
*
* Last modified: January 14th 2000
* 
* This simplified models accounts for propagation delays 
* and includes the complete overcharge detection scheme
*
* ------------------------------------------------------------ *
**********
*$
.SUBCKT NCP1200/ON 1   2  3  4   5   6      8 PARAMS: Fs=40k
*                  Adj FB CS Gnd Drv Vcc   HV
**** ESD NETWORK ****
D1 4 1 DCLAMP1
D2 4 2 DCLAMP1
D3 4 3 DCLAMP1
D4 4 5 DCLAMP2
D5 4 6 DCLAMP2
**** SELF-SUPPLY ****
GB2 8 6 VALUE = { IF ( V(11) > 3.5, 0, 4m ) }
XCOMP1 6 10 UVLO COMP2_1200
EB5 10 4 VALUE = { IF ( V(UVLO) < 3.5, 12, 10 ) }
X2 UVLO OVERLATCH 11 OR2_1200
XUVLO6 100 6 110 COMP2_1200
X50 110 OVERLATCH UVLO6 AND2_1200
VUVLO6 100 4 6.5
**** OUTPUT PULSE VALIDATION ****
XPULS UVLO OVER PULSOK NC4 TFFLOP_1200 ; turns the pulses on at UVLOh
* S R Q Q\
RDUM4 OVER 4 100k
**** CURRENT CONSUMPTION ****
ISUPP1 6 4 300u
GBSUPP2 6 4 VALUE = { IF ( V(PULSOK) > 3.5, (300u + I(VFBM)), 0 ) }
GBSUPPDRV 6 4 VALUE = { IF ( I(VPW) > 0, I(VPW), 0 ) }
**** CLOCK SECTION ****
VCLOCK clk1 4 PULSE 0 10 0 10n 10n 100n {1/Fs}
VMDUTY MDUTY 4 PULSE 0 10 {0.8/Fs} 10n 10n 100n {1/Fs}
XRESET clk1 200 clk2 AND2_1200
EBINVRST 200 4 VALUE = { IF ( V(SKIP)<800mV, 3.5, 0 ) }
XFFLOP clk2 RST Q NC1 TFFLOP_1200
RNC1 NC1 4 100k
XRST MDUTY IMAX SKIP RST OR3_1200
**** CURRENT SENSE ****
EBVREF REF1 4 VALUE = { IF ( V(6) > 4.8V, 4.8, V(6) ) } 
VFBM REF1 REF ; feedback current measure
RFB REF 2 8k
RDUPP 2 290 57k
RDLOW 290 4 20k
EBCLIP ISET 4 VALUE = {IF ( V(290) > 1, 1V, V(290) ) }
XCOMP2 LEBO ISET OVERI COMP2_1200
XPROPDEL OVERI IMAX UTD_1200 PARAMS: TD={200ns} ; propagation delay
XDEL3 Q 30 UTD_1200 PARAMS: TD={250ns}
EBLEB LEBO 4 VALUE = { IF ( V(30) > 3V, V(3), 0 ) }
**** OVERLOAD SECTION ****
EBOVER 50 4 VALUE = { IF ( (V(ISET)>990mV) & (V(PULSOK)>3.5), 10, 0 ) }
XOVER UVLO 50 OVER AND2I_1200 ; UVLO low and OVL, latches off the pulses
XLATCH OVER UVLO6 OVERLATCH NC5 TFFLOP_1200 ; latches off the current source
RNC5 NC5 0 100k
**** SKIP CYCLE SECTION ****
RADJU REF 1 70k
RADJL 1 4 23.3k
XSKIP 150 2 SKIP COMP2_1200
EBHYST 150 4 VALUE = { IF ( V(SKIP) < 3.5, V(1), V(1)+100m ) }
**** DRIVER SECTION ****
EBDRVU 90 4 VALUE = { IF ( (V(PULSOK)>3V) & (V(Q)>3V), V(6), 100mV ) }
VPW 90 91
RON 91 5 36
ROFF 90 250 18
DOFF 5 250 DSTD
****
.MODEL DSTD D RS=100M CJO=15pF TT=10N IBV=100u N=0.2
.MODEL DCLAMP1 D BV=10
.MODEL DCLAMP2 D BV=20
.ENDS
*$
**** SUBCIRCUIT MODELS ****
.SUBCKT UTD_1200 1 2 PARAMS: TD=200n
*
*Parameters K=GAIN TD=DELAY
RIN 1 0 1E15
E1 3 0 1 0 1
T1 3 0 2 0 Z0=1 TD={TD}
R1 2 0 1
.ENDS
*$
**** 2 INPUT COMPARATOR ****
.SUBCKT COMP2_1200 1 2 3
*                  + - S
EB1 4 0 VALUE = { IF ( V(1) > V(2), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
**** 4 INPUT OR GATE ****
.SUBCKT OR3_1200 1 2 3 6
EB1 5 0 VALUE = { IF ( (V(1)>800M) | (V(2)>800M) | (V(3)>800M), 5V, 0 ) }
RD 5 6 100
CD 6 0 10P
.ENDS 
*$
**** 2 INPUT OR GATE ****
.SUBCKT OR2_1200 1 2 3
EB1 4 0 VALUE = { IF ( (V(1)>800M) | (V(2)>800M), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2_1200 1 2 3
EB1 4 0 VALUE = { IF ( (V(1)>800M) & (V(2)>800M), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
**** 2 INPUT AND CIRCUIT ****
.SUBCKT AND2I_1200 1 2 3
EB1 4 0 VALUE = { IF ( (V(1)<800M) & (V(2)>800M), 5V, 0 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
**** 2 INPUT NOR CIRCUIT ****
.SUBCKT NOR2_1200 1 2 3
EB1 4 0 VALUE = { IF ( (V(1)>800M) | (V(2)>800M), 0V, 5 ) }
RD 4 3 100
CD 3 0 10P
.ENDS 
*$
****
.SUBCKT TFFLOP_1200 6 8 2 1
*              S R Q Q\
EBQB   10 0 VALUE = { IF ( (V(8)<800M) & (V(2)>800M), 0, 10V ) }
EBQ    20 0 VALUE = { IF ( (V(6)<800M) & (V(1)>800M), 0, 10V ) }
RD1   10 1 100
CD1   1 0 10p IC=5
RD2   20 2 100
CD2   2 0 10p IC=0
.ENDS 
*******
***** END OF NCP1200 INTERNAL MODELS *****
*************
* ON Semiconductor
* NCP1200 model developed by Christophe BASSO, Toulouse (FRANCE)
* e-mail: christophe.basso@onsemi.com
* OrCAD PSpice compatible
*
* Last modified: January 14th 2000
* NCP1200 average FLYBACK AC model DCM and CCM modes
* FLYBACK average model made by Ben-Gurion University (ISRAEL)
* http://www.ee.bgu.ac.il/~pel
*************
.SUBCKT NCP1200_Av/ON In Out Gnd FB CTRL PARAMS: FS=40k L=2.7m MC=1 RI=2.7
GIN IN GND VALUE = { I(VLM)*V(DON)/(V(DON)+V(DOFF)) }
ELM OUT1 GND VALUE = { V(IN)*V(DON)-V(OUT)*V(DOFF) }
RM OUT1 5 1M
LM 5 8 {L}
VLM 8 GND
GOUT GND OUT VALUE = { I(VLM)*V(DOFF)/(V(DON)+V(DOFF)) }
VCLP VC 0 9M
D2 VC DOFF DBREAK
D1 DOFF 6 DBREAK
R4 DOFF 7 10
EDOFFM 6 GND VALUE = { 1-V(DON)-9M }
EDOFF 7 GND VALUE = { 2*I(VLM)*{FS}*{L}/(V(DON))/(V(IN))-V(DON) }
EDON  DON GND TABLE { ((V(CONTROL)-I(VLM)*{RI})*{FS})/({MC}+((0.5*{RI}*V(IN))/{L})) } 
+ ( (10m,10m) (.999,.999) )
EB1 CONTROL 0 VALUE = { IF ( V(CTRL)/4 > 1, 1, V(CTRL)/4 ) }
Vref 50 GND DC=4.8V
Rfb 50 FB 8k
.MODEL DBREAK D (TT=1N CJO=10P N=0.01)
.ENDS
*$
* ------------------------------------------------------------ *
*
* ON Semiconductor
* MC44608 model developed by Christophe BASSO, Toulouse (FRANCE)
* e-mail: christophe.basso@onsemi.com
* OrCAD PSpice compatible
*
* Last modified: April 9th 1999
* NCP1200 average FLYBACK AC model DCM and CCM modes
* FLYBACK average model made by Ben-Gurion University (ISRAEL)
* http://www.ee.bgu.ac.il/~pel
*
* ------------------------------------------------------------ *
*********
.SUBCKT MC44608_AV/ON 77      81       11 31  51  61 PARAMS: Lp=180u Fs=75k
*                    In_duty Duty_Out FB Gnd Out Vin


RGAIN1 11 70 20                 ; Dynamic zener resistance
XERR 71 70 72 ERRAMP_44608	  ; Internal error amplifier
VREF 71 31 5V                   ; Internal reference voltage
XPCH 73 72 70 PCHAN_44608       ; P-channel MOSFET
RGAIN2 73 31 390                ; I/V converter
DCLP 73 74 DCLAMP               ; Negative clamp
VCLP1 74 31 320mV               ; lower duty-cycle limit
VD 75 31 820mV                  ; upper duty-cycle limit
E1 76 31 75 73 1                ; decreasing duty-cycle
RF 76 140 750k
CF 140 31 55pF
EB2 81 31 VALUE = { IF ( V(140,31)>810mV,810mV, IF ( V(140,31)<10mV,10mV, V(140,31) ) ) }
EELM OUT1 31 VALUE = { V(61)*V(77)-V(51)*V(DOFF) }
EDOFF 7 31 VALUE = { 2*I(VLM)*{FS}*{Lp}/V(77)/V(61)-V(77) }
GIN 61 31 VALUE = { I(VLM)*V(77)/(V(77)+V(DOFF)) }
GOUT 31 51 VALUE = { I(VLM)*V(DOFF)/1/(V(77)+V(DOFF)) }
RM OUT1 5 1M
LM 5 8 {Lp}
VLM 8 31
VCLP2 VC 31 9M
D2 VC DOFF DBREAK
D1 DOFF 6 DBREAK
R4 DOFF 7 10
EDOFFM 6 31 VALUE = { 1-V(77)-9M }
.MODEL DBREAK D (TT=10N CJO=100P N=0.01 RS=10M)
.MODEL DCLAMP D (BV=100V CJO=4PF IS=7E-09 M=.45 N=1.8
+ RS=1.5 TT=6E-09 VJ=.6V)
.ENDS 
*********
.SUBCKT PCHAN_44608 30 40 50
* NODES: DRAIN GATE SOURCE
M1 30 20 50 50 MOD1 L=1U W=1U
RG 40 20 167
RL 30 50 50E6
.MODEL MOD1 PMOS VTO=-1.709 RS=3.091 RD=0.979 IS=1E-15 KP=0.146
+CGSO=26P CGDO=4P CBD=12P PB=1
.ENDS
**** SIMPLE ERROR AMPLIFIER ****
.SUBCKT ERRAMP_44608 1 5 2
*                    + - OUT
G1 0 4 1 5 100u
R1 4 0 300Meg   ; 90dB OL
C1 4 0 53pF             ; 10Hz pole
E1 2 0 4 0 1
V1 3 0 500mV
D2 3 4 DCLP
.MODEL DCLP D (BV=8.7 CJO=40PF IS=7E-09 M=.45 N=2 RS=.8)
+ TT=10E-09 VJ=.6V)
.ENDS 
*****
***** END OF mc44608 INTERNAL MODELS *****
